@SuppressWarnings('PMD.ApexCRUDViolation')
//Incident #64 - https://confluence.nct.org.uk:8443/x/EoSO
public without sharing class AddMarketingPreferencesToCare {

    private static final Map<String, String> MARKETING_PREFERENCES_DOCUMENT_MAP = new Map<String, String> {
        'Has_Opted_In_Email__c' => 'GDPR007',
        'Has_Opted_In_SMS__c' => 'GDPR008',
        'Has_Opted_In_Post__c' => 'GDPR009',
        'Has_Opted_In_Telephone__c' => 'GDPR010'
    };

    @InvocableMethod
    public static void addMarketingPreferences(List<Params> paramInstance) {
        List<Contact> contactList = [
            SELECT  Nct_Number__c,
                    Individual.Has_Opted_In_Email__c,
                    Individual.Has_Opted_In_SMS__c,
                    Individual.Has_Opted_In_Telephone__c,
                    Individual.Has_Opted_In_Post__c
            FROM Contact
            WHERE(Id =: paramInstance[0].contactId AND Nct_Number__c != null)
            LIMIT 1
        ];

        if(!contactList.isEmpty()) {
            for(String individualField : MARKETING_PREFERENCES_DOCUMENT_MAP.keySet() ) {
                CareHelper.sendMarketingPreferences(contactList[0].Nct_Number__c, MARKETING_PREFERENCES_DOCUMENT_MAP.get(individualField), (Boolean)contactList[0].Individual.get(individualField));
            }
        }
    }

    public class Params {
        @InvocableVariable public String contactId;
    }
}